// define a global bounding box (s,w,n,e)
[bbox:43.56,-79.59,43.78,-79.16];

// Old Toronto boundary by id for clipping the data later
rel(2989349)->.TO;
rel(2989349); map_to_area->.TOarea;


// select ways and relations of interest from within the bounding box
(

	way[waterway~'river*'];
	rel[waterway=riverbank];
  
	way[natural~'wood|beach|wetland|water'];
	rel[natural=water];
	node[natural=tree];

	way[leisure~'^park$|golf_course|nature_reserve'];
	rel[leisure~'^park$|golf_course|nature_reserve'];
  
	way[landuse~'cemetery|industrial|railway|forest|retail'];

	way[building=retail];
	rel[building=retail];

	way[amenity=university];
	rel[amenity=university];

	rel[route=train][operator~'GO Transit|Union Pearson Express'];

	way[aeroway~'runway|aerodrome|taxiway|terminal'];
	rel[aeroway~'aerodrome|terminal'];

	nwr[shop=bicycle];
	nwr[amenity=bicycle_rental];

	.TO; // the city relation itself

)->.prefilter;

// filter out data we don't want
(
	( // keep everything except CanVec sources
		nwr.prefilter[!source];
		nwr.prefilter[source!~'CanVec'];
	);
	-
	( // exclude two giant lakes from the above set
		rel[name='Lake Huron'];
		rel[name='Lake Erie'];
	);
)->.filtered;

// clip to Old Toronto
nwr.filtered(area.TOarea)->.clipped;

(.clipped;>;); 

out body qt;
