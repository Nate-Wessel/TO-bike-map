// define a global bounding box (s,w,n,e)
[bbox:43.5,-79.7,43.9,-79.0];

// Toronto (city) by id for clipping the data later
rel(324211)->.TO;
rel(324211); map_to_area->.TOarea;


// select ways and relations of interest from within the bounding box
(

	way[waterway~'river*'];
	rel[waterway=riverbank];
  
	way[natural~'wood|beach|wetland|water'];
	rel[natural=water];
	node[natural=tree];

	way[leisure~'^park$|golf_course'];
	rel[leisure~'^park$|golf_course'];
  
	way[landuse~'cemetery|industrial|railway|forest|retail'];

	way[amenity=university];
	rel[amenity=university];

	rel[route=train][operator~'GO Transit|Union Pearson Express'];

	way[aeroway~'runway|aerodrome|taxiway|terminal'];
	rel[aeroway~'aerodrome|terminal'];

	nwr[shop=bicycle];
	nwr[amenity=bicycle_rental];

	.TO; // the city relation itself

)->.prefilter;

// filter out data we don't want
(
	( // keep everything except CanVec sources
		nwr.prefilter[!source];
		nwr.prefilter[source!~'CanVec'];
	);
	-
	( // exclude two giant lakes from the above set
		rel[name='Lake Huron'];
		rel[name='Lake Erie'];
	);
)->.filtered;

( // clip to anything in or around Toronto
	nwr.filtered(area.TOarea);    // in
	nwr.filtered(around.TO:5000); // around
)->.clipped;

(.clipped;>;); 

out body qt;
