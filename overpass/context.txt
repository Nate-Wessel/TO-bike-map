// define a global bounding box (s,w,n,e)
[bbox:42.8,-80.8,45.0,-77.9];

// select ways and relations of interest from within the bounding box
(

	way[waterway~'river'];
	rel[waterway=riverbank];
  
	way[natural~'wood|beach|wetland|water'];
	rel[natural=water];

	way[leisure~'^park$|golf_course'];
  
	way[landuse~'cemetery|industrial|railway|forest'];

	way[amenity=university];
	rel[amenity=university];

)->.prefilter;

// filter out data we don't want
(
	( // keep everything except CanVec sources
		nwr.prefilter[!source];
		nwr.prefilter[source!~'CanVec'];
	);
	-
	( // exclude two giant lakes from the above set
		rel[name='Lake Huron'];
		rel[name='Lake Erie'];
	);
)->.filtered;

(.filtered;>;); 

out body qt;
